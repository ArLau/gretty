plugins {
  id 'grettybuild.library'
}

import org.apache.tools.ant.filters.*

dependencies {
  // #231 If we decide to drop Gradle 6 support / Groovy 2 support, we can drop choosing Groovy versions at runtime again.
  compileOnly localGroovy()

  api "ch.qos.logback:logback-classic:$logback_version"
  api 'commons-cli:commons-cli:1.3.1'
  api 'commons-configuration:commons-configuration:1.10'
  api 'commons-io:commons-io:2.4'
  api 'org.apache.commons:commons-lang3:3.3.2'
  api 'org.bouncycastle:bcprov-jdk15on:1.60'
  api "org.springframework.boot:spring-boot-devtools:$springBootVersion"
  api project(':libs:gretty-common')
}

tasks.processResources {
  def props = [
    grettyVersion: project.version,
    jetty7Version: project.jetty7_version,
    jetty7ServletApiVersion: project.jetty7_servlet_api_version,
    jetty8Version: project.jetty8_version,
    jetty8ServletApiVersion: project.jetty8_servlet_api_version,
    jetty9Version: project.jetty9_version,
    jetty93Version: project.jetty93_version,
    jetty94Version: project.jetty94_version,
    jetty9ServletApiVersion: project.jetty9_servlet_api_version,
    tomcat85Version: project.tomcat85_version,
    tomcat85ServletApiVersion: project.tomcat85_servlet_api_version,
    tomcat9Version: project.tomcat9_version,
    tomcat9ServletApiVersion: project.tomcat9_servlet_api_version,
    springBootVersion: project.springBootVersion,
    springLoadedVersion: project.springLoadedVersion,
    springVersion: project.springVersion,
    logbackVersion: project.logback_version,
    asmVersion: project.asm_version
  ]
  inputs.properties props
  filter ReplaceTokens, tokens: props
}
