import org.gradle.util.GradleVersion

plugins {
  id 'grettybuild.plugin'
  id 'com.gradle.plugin-publish' version '0.15.0'
}

dependencies {
  api project(':libs:gretty-core'), {
    // gradle API provides it's own versions of groovy-all and logback.
    exclude group: 'org.codehaus.groovy', module: 'groovy-all'
    exclude group: 'ch.qos.logback', module: 'logback-classic'
  }
  api "org.springframework.boot:spring-boot-loader-tools:${springBootVersion}"
  api "org.eclipse.jetty:jetty-util:$jetty8_version"
  api gradleApi()

  testImplementation "ch.qos.logback:logback-classic:$logback_version"
}

tasks.withType(Test).configureEach {
  if (GradleVersion.current().baseVersion.version.startsWith("7.")) {
    useJUnitPlatform()
  }
}

gradlePlugin {
  plugins {
    grettyPlugin {
      id = 'org.gretty'
      implementationClass = 'org.akhikhl.gretty.GrettyPlugin'
    }
  }
}

pluginBundle {
  website = 'https://gretty-gradle-plugin.github.io/gretty-doc/'
  vcsUrl = "https://github.com/${developerId}/gretty"
  description = 'Advanced gradle plugin for running web-apps on jetty and tomcat.'

  plugins {
    grettyPlugin {
      displayName = 'Gretty plugin'
      tags = [ 'gretty', 'jetty', 'tomcat', 'gradle', 'plugin', 'spring', 'spring-boot' ]
    }
  }
}

validatePlugins {
  enableStricterValidation = true
  // FIXME #235 - Pass stricter validation since Gradle 7.2
  // see PR #236 for the required changes
  //
  // We can't ship the improved plugin because passing the plugin validation requires
  // adding an annotation which is not shipped with Gradle 6.9.
  // As a result, we need to drop Gradle 6 compat, then add the annotation only present
  // in Gradle 7.1 onwards, and then we can enable strict validation again.
  //
  // Unluckily, there is no way to selectively disable this validation for the Gradle 7
  // build. Also, faking the presence of the annotation to get the plugin to compile with
  // Gradle 6 would complicate the build process by a lot.
  ignoreFailures = true
}

tasks.named('publishPlugins').configure {
  dependsOn assertPublishedVersion
}
